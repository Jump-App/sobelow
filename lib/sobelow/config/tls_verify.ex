defmodule Sobelow.Config.TLSVerify do
  @moduledoc """
  # Disabled TLS certificate verification

  Reports literal `verify: :verify_none` in TLS options, including Gun's
  `tls_opts` map entries. It also catches hackney's `:insecure` option and
  the direct `verify` option in WebTransport connections. Disabling
  verification lets an attacker impersonate the remote server. Use
  `:verify_peer` and a trusted CA, including for self-signed services.

  Configuration in `dev.exs` and `test.exs` is excluded with the rest of
  Sobelow's config checks. Source files are scanned regardless of the
  runtime environment; review guards around development-only calls.
  Dynamically constructed options are not resolved.

  Ignore this check with:

      $ mix sobelow -i Config.TLSVerify
  """
  @uid 34
  @finding_type "Config.TLSVerify: TLS Certificate Verification Disabled"
  @tls_option_keys [:ssl, :ssl_options, :transport_opts, :connect_options, :tls, :tls_opts]

  use Sobelow.Finding

  def run(dir_path, configs, files) do
    paths = Enum.map(configs, &Path.join(dir_path, &1)) ++ Enum.map(files, & &1.file_path)

    Enum.each(paths, fn path ->
      path
      |> Parse.ast()
      |> insecure_calls()
      |> Enum.each(&add_finding(path, &1))
    end)
  end

  @doc false
  def insecure_calls(ast) do
    {_, calls} =
      Macro.prewalk(ast, [], fn
        {name, _, args} = call, acc when is_list(args) and name != :%{} ->
          if insecure_options?(args, hackney_call?(name), direct_tls_options?(name)) do
            {call, [call | acc]}
          else
            {call, acc}
          end

        node, acc ->
          {node, acc}
      end)

    Enum.reverse(calls)
  end

  defp insecure_options?(options, hackney?, tls?) when is_list(options) do
    Enum.any?(options, &insecure_option?(&1, hackney?, tls?))
  end

  defp insecure_options?(_, _, _), do: false

  defp insecure_option?(options, hackney?, tls?) when is_list(options),
    do: insecure_options?(options, hackney?, tls?)

  defp insecure_option?({:%{}, _, entries}, hackney?, tls?) when is_list(entries),
    do: insecure_options?(entries, hackney?, tls?)

  defp insecure_option?({:verify, :verify_none}, _, tls?), do: tls?

  defp insecure_option?({key, options}, hackney?, tls?) when is_list(options),
    do: insecure_options?(options, hackney? or key == :hackney, tls? or key in @tls_option_keys)

  defp insecure_option?(:insecure, true, _), do: true
  defp insecure_option?(_, _, _), do: false

  defp hackney_call?({:., _, [:hackney, _]}), do: true
  defp hackney_call?(_), do: false

  defp direct_tls_options?({:., _, [:ssl, _]}), do: true
  defp direct_tls_options?({:., _, [:hackney, :wt_connect]}), do: true
  defp direct_tls_options?(_), do: false

  defp add_finding(path, call) do
    finding =
      Finding.init(@finding_type, Utils.normalize_path(path), :high)
      |> Map.merge(%{
        vuln_source: :highlight_all,
        vuln_line_no: Parse.get_fun_line(call),
        vuln_col_no: Parse.get_fun_column(call),
        fun_source: call
      })

    Print.add_finding(finding)
  end
end
